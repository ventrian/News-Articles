DROP PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetAuthorStatistics
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetAuthorStatistics
	@ModuleID	int, 
	@CategoryID 	varchar(255),
	@SortBy	varchar(255),
	@ShowPending bit   
AS

DECLARE @strTop	nvarchar(2000)
DECLARE @strWhere	nvarchar(2000)

SELECT @strTop = ''
SELECT @strWhere = ''

IF (@ModuleID is not null)
	SELECT @strWhere = @strWhere + ' AND Articles.ModuleID = ' + convert(nvarchar, @ModuleId)
	
IF (@CategoryId is not null)
	SELECT @strWhere = @strWhere + ' AND Articles.ArticleID in (select ArticleID from {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories ArticleCategories where ArticleCategories.CategoryID in (' + @CategoryID + '))'

IF (@ShowPending is null )
    SELECT @strWhere = @strWhere + ' AND (Articles.StartDate is null OR Articles.StartDate < ''' + convert(nvarchar, DateAdd(mi, 1, GetDate()))  + ''')'

EXEC('
SELECT     
	UserID, UserName, DisplayName, FirstName, LastName, count(*) as ''ArticleCount''   
FROM         
	{databaseOwner}{objectQualifier}Users Users, {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Article Articles
where Articles.IsApproved = 1 and Articles.IsDraft = 0 and Users.UserID = Articles.AuthorID ' + 
	@strWhere + ' 
GROUP BY 
	UserID, UserName, DisplayName, FirstName, LastName
ORDER BY ' +
	@SortBy)
GO

DROP PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetCategoryListAll
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetCategoryListAll 
	@ModuleID int,
	@Current int, 
	@CategoryID varchar(255), 
	@MaxDepth int,
	@ShowPending bit   
AS

SET NOCOUNT ON

DECLARE @FilterID int
SET @FilterID = @Current

DECLARE @level int, @line int
 
CREATE TABLE #hierarchy(HierarchyID int IDENTITY (1,1), CategoryID int, level int)
CREATE TABLE #stack (item int, level int)
INSERT INTO #stack VALUES (@current, 1)
SELECT @level = 1
 
WHILE @level > 0
BEGIN
   IF EXISTS (SELECT * FROM #stack WHERE level = @level)
      BEGIN
         SELECT @current = item
         FROM #stack
         WHERE level = @level
 
 		insert into #hierarchy(CategoryID, level) values(@current, @level) 
 
         DELETE FROM #stack
         WHERE level = @level
            AND item = @current
            
 		 IF( @MaxDepth IS NULL OR @MaxDepth >= @level )
		 BEGIN
             INSERT #stack
                SELECT CategoryID, @level + 1
                FROM {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Category
                WHERE ParentID = @current and ModuleID = @ModuleID 
     	        ORDER BY [SortOrder] DESC, [Name] DESC

             IF @@ROWCOUNT > 0
                SELECT @level = @level + 1
		 END
      END
   ELSE
      SELECT @level = @level - 1
END -- WHILE

IF( @FilterID IS NOT NULL )
BEGIN
	DELETE FROM #hierarchy WHERE CategoryID = @FilterID
END

DECLARE @strModuleID	nvarchar(50)
SELECT @strModuleID = convert(varchar, @ModuleID)

DECLARE @strCategoryId	nvarchar(255)
IF (@CategoryId is not null)
	SELECT @strCategoryId = ' AND c.CategoryID in (' + @CategoryID + ')'
	
DECLARE @strPending nvarchar(2000)
IF (@ShowPending is null) 
	SELECT @strPending = ' AND (a.StartDate IS NULL OR a.StartDate < GetDate())'

EXEC('
SELECT
	c.[CategoryID],
	c.[ParentID],
	c.[ModuleID],
	c.[Name],
	REPLICATE(''.'',(level-2)*2) + c.[Name] as ''NameIndented'',
	c.[Image],
	c.[Description],
	c.[SortOrder],
	level-1 as ''Level'',
	(SELECT COUNT(*) FROM {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories ac INNER JOIN {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Article a ON ac.ArticleID = a.ArticleID WHERE ac.CategoryID = c.CategoryID AND a.IsDraft = 0 AND a.IsApproved = 1 ' + @strPending + ') as ''NumberofArticles'',
	(SELECT SUM(a.NumberOfViews) FROM {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Article a INNER JOIN {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories ac ON a.ArticleID = ac.ArticleID WHERE ac.CategoryID = c.CategoryID) as ''NumberofViews''
FROM
	#hierarchy h INNER JOIN
		{databaseOwner}{objectQualifier}DnnForge_NewsArticles_Category c ON h.CategoryID = c.CategoryID 
WHERE
	c.[ModuleID] = ' + @strModuleID + @strCategoryId + ' 
ORDER BY 
	h.HierarchyID ASC')

drop table #hierarchy
drop table #stack
GO

DROP PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetNewsArchive
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetNewsArchive
	@ModuleID int, 
	@CategoryID varchar(255),
	@GroupBy varchar(255),
	@ShowPending bit   
AS

DECLARE @strTop	nvarchar(2000)
DECLARE @strWhere	nvarchar(2000)

SELECT @strTop = ''
SELECT @strWhere = ''

IF (@ModuleID is not null)
	SELECT @strWhere = @strWhere + ' AND Article.ModuleID = ' + convert(nvarchar, @ModuleId)
	
IF (@CategoryId is not null)
	SELECT @strWhere = @strWhere + ' AND Article.ArticleID in ( select ArticleID from {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories where {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories.ArticleID = Article.ArticleID and {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories.CategoryID in (' + @CategoryID + '))'

IF (@ShowPending is null)
BEGIN
	SELECT @strWhere = @strWhere + ' AND (Article.StartDate is null or Article.StartDate < ''' + convert(nvarchar, DateAdd(mi, 1, GetDate()))  + ''')'
END

IF( @GroupBy = 'Month' )
BEGIN
EXEC('
    select Month(StartDate) as [Month], Year(StartDate) as [Year], 1 as Day, Count(*) as [Count]
    from {databaseOwner}{objectQualifier}dnnforge_newsarticles_article Article
    where IsApproved = 1 and IsDraft = 0 ' + 
	    @strWhere + '
    group by Year(StartDate), Month(StartDate)
    order by [Year] desc, [Month] desc')
END
ELSE
BEGIN
EXEC('
    select 1 as [Month], Year(StartDate) as [Year], 1 as Day, Count(*) as [Count]
    from {databaseOwner}{objectQualifier}dnnforge_newsarticles_article Article
    where IsApproved = 1 and IsDraft = 0 ' + 
	    @strWhere + '
    group by Year(StartDate)
    order by [Year] desc')
END
GO

DROP PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetArticleListBySearchCriteria
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}DnnForge_NewsArticles_GetArticleListBySearchCriteria
	@ModuleID	int,
	@CurrentDate datetime,
	@AgedDate datetime,
	@CategoryID	varchar(255),
	@CategoryIDCount	int,
	@MaxCount int, 
	@PageNumber int, 
	@PageSize int,
	@SortBy varchar(50),
	@SortDirection varchar(50),
	@IsApproved	bit,
	@IsDraft	bit,
	@KeyWords	varchar(255), 
	@AuthorID	int,
	@ShowPending bit,
	@ShowExpired bit,
	@ShowFeaturedOnly bit,
	@ShowNotFeaturedOnly bit,
	@ShowSecuredOnly bit,
	@ShowNotSecuredOnly bit,
	@ArticleIDs	varchar(255)
AS

-- Form Where Query

DECLARE @strWhere	nvarchar(4000)
SELECT @strWhere = 'Article.ModuleID = ' + convert(nvarchar, @ModuleId)

IF( @CurrentDate is null )
	SELECT @CurrentDate = GetDate()

IF( @ShowPending is null )
BEGIN
	SELECT @strWhere = @strWhere + ' AND (Article.StartDate is null OR (Article.StartDate < ''' + convert(nvarchar, @CurrentDate)  + ''' AND Article.StartDate < GetDate()))'
	
	IF( @AgedDate is not null )
		SELECT @strWhere = @strWhere + ' AND Article.StartDate > ''' + convert(nvarchar, @AgedDate)  + ''''
END
ELSE
BEGIN
	IF( @CurrentDate is not null AND @AgedDate is not null )
	BEGIN
		SELECT @strWhere = @strWhere + ' AND (Article.StartDate is null OR (Article.StartDate < ''' + convert(nvarchar, @CurrentDate)  + '''))'
		
		IF( @AgedDate is not null )
			SELECT @strWhere = @strWhere + ' AND Article.StartDate > ''' + convert(nvarchar, @AgedDate)  + ''''
	END
END

IF( @ShowExpired is null )
BEGIN
	SELECT @strWhere = @strWhere + ' AND (Article.EndDate is null OR Article.EndDate > ''' + convert(nvarchar, @CurrentDate)  + ''')'
END

IF (@CategoryId is not null)
	if(@CategoryIDCount is not null)
		SELECT @strWhere = @strWhere + ' AND Article.ArticleID in ( select ArticleID from {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories where {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories.ArticleID = Article.ArticleID and {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories.CategoryID in (' + @CategoryID + ') group by articleID having count(*) > ' + convert(nvarchar, @CategoryIDCount) + ')'
	else
		SELECT @strWhere = @strWhere + ' AND Article.ArticleID in ( select ArticleID from {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories where {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories.ArticleID = Article.ArticleID and {databaseOwner}{objectQualifier}DnnForge_NewsArticles_ArticleCategories.CategoryID in (' + @CategoryID + '))'

IF (@AuthorID is not null)
	SELECT @strWhere = @strWhere + ' AND Article.AuthorID = ' + convert(nvarchar, @AuthorID)

IF (@IsApproved = 1 )
	SELECT @strWhere = @strWhere + ' AND Article.IsApproved = 1'

IF (@IsApproved = 0 )
	SELECT @strWhere = @strWhere + ' AND Article.IsApproved = 0'

IF (@IsDraft = 0)
	SELECT @strWhere = @strWhere + ' AND Article.IsDraft = 0'

IF (@IsDraft = 1 )
	SELECT @strWhere = @strWhere + ' AND Article.IsDraft = 1'

IF (@ShowFeaturedOnly = 1 )
	SELECT @strWhere = @strWhere + ' AND Article.IsFeatured = 1'
	
IF (@ShowNotFeaturedOnly = 1 )
	SELECT @strWhere = @strWhere + ' AND Article.IsFeatured = 0'

IF (@ShowSecuredOnly = 1 )
	SELECT @strWhere = @strWhere + ' AND Article.IsSecure = 1'
	
IF (@ShowNotSecuredOnly = 1 )
	SELECT @strWhere = @strWhere + ' AND Article.IsSecure = 0'

IF (@Keywords is not null)
	SELECT @strWhere = @strWhere + ' AND (Article.Title LIKE ''%' + @Keywords + '%'' OR Article.Summary LIKE ''%' + @Keywords + '%'' OR ((select count(*) from {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Page Page where Page.ArticleID = Article.ArticleID and Page.PageText LIKE ''%' + @Keywords + '%'') > 0))'

IF (@ArticleIDs is not null)
	SELECT @strWhere = @strWhere + ' AND Article.ArticleID in (' + @ArticleIDs + ')'
	
IF (@SortBy = 'Title') 
	SELECT @SortBy = 'Article.Title'

IF (@SortBy = 'IsFeatured DESC, Title') 
	SELECT @SortBy = 'IsFeatured DESC, Article.Title'

-- Set Paging Options

IF( @PageNumber is null )
	SET @PageNumber = 1

IF( @PageSize is null )
	SET @PageSize = 100

SET @PageNumber = @PageNumber - 1

DECLARE @startRowIndex int
SET @startRowIndex = (@PageNumber * @PageSize)

DECLARE @maxRow int
SET @maxRow = (@startRowIndex + @PageSize)
IF( @MaxCount is not null )
BEGIN
	IF( @MaxCount < @MaxRow )
	BEGIN
		SET ROWCOUNT @MaxCount
	END
	ELSE
	BEGIN
		SET ROWCOUNT @maxRow
	END
END
ELSE
BEGIN
	SET ROWCOUNT @maxRow
END

-- Create Temporary Table

CREATE TABLE #TempItems 
(
   ID int IDENTITY,
   ArticleID int
)

EXEC('
INSERT INTO #TempItems (ArticleID) 
SELECT 
	Article.[ArticleID]
FROM
	{databaseOwner}{objectQualifier}DnnForge_NewsArticles_Article Article LEFT OUTER JOIN 
    {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Page Page on Page.ArticleID = Article.ArticleID AND Page.SortOrder = 0 LEFT OUTER JOIN 
	{databaseOwner}{objectQualifier}Users Author ON Article.AuthorID = Author.UserID LEFT OUTER JOIN 
    {databaseOwner}{objectQualifier}Users Updated ON Article.LastUpdateID = Updated.UserID
WHERE ' + 
	@strWhere + '
ORDER BY 
	' + @SortBy + ' ' + @SortDirection)

SET ROWCOUNT 0

SELECT 
	Article.[ArticleID],
	Article.[AuthorID],
	Article.[ApproverID],
	Article.[CreatedDate],
	Article.[LastUpdate],
	Article.[Title],
	Article.[Summary],
	Article.[IsApproved],
	Article.[NumberOfViews],
	Article.[StartDate],
	Article.[EndDate],
	Article.[ModuleID],
	Article.[IsFeatured],
	Article.[ImageUrl],
	Article.[LastUpdateID],
	Article.[IsSecure],
	Article.[IsNewWindow],
	Article.[ImageUrl],
	Article.[Url],
	Article.[PageCount],
	Article.[CommentCount],
	Article.[Rating],
	Article.[RatingCount],
	Author.[Email] as AuthorEmail,
	Author.[UserName] as AuthorUserName,
	Author.[FirstName] as AuthorFirstName,
	Author.[LastName] as AuthorLastName,
	Author.[DisplayName] as AuthorDisplayName,
	Updated.[Email] as LastUpdateEmail,
	Updated.[UserName] as LastUpdateUserName,
	Updated.[FirstName] as LastUpdateFirstName,
	Updated.[LastName] as LastUpdateLastName,
	Updated.[DisplayName] as LastUpdateDisplayName,
	Page.PageText as 'Body'

FROM
	#TempItems t INNER JOIN 
	{databaseOwner}{objectQualifier}DnnForge_NewsArticles_Article Article ON t.ArticleID = Article.ArticleID LEFT OUTER JOIN 
    {databaseOwner}{objectQualifier}DnnForge_NewsArticles_Page Page on Page.ArticleID = Article.ArticleID AND Page.SortOrder = 0 LEFT OUTER JOIN 
	{databaseOwner}{objectQualifier}Users Author ON Article.AuthorID = Author.UserID LEFT OUTER JOIN 
    {databaseOwner}{objectQualifier}Users Updated ON Article.LastUpdateID = Updated.UserID

WHERE
	ID > @startRowIndex
	
ORDER BY
	ID

DROP TABLE #TempItems 

EXEC('
SELECT 
	COUNT(Article.ArticleID) as ''TotalRecords'' 
FROM
	{databaseOwner}{objectQualifier}DnnForge_NewsArticles_Article Article
WHERE ' + 
	@strWhere )
GO
